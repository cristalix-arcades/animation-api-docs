//file:noinspection GroovyAssignabilityCheck
//file:noinspection DifferentStdlibGradleVersion
plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
}

compileJava {
    options.encoding = "UTF-8"
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

configurations {
    shade
    compileClasspath.extendsFrom(shade)
}

dependencies {
    shade project(':protocol')
    shade project(':graffiti-protocol')

    compileOnlyApi 'cristalix:dark-paper:21.02.03'
    api 'cristalix:bukkit-core:21.01.30'
    api 'org.jetbrains.kotlin:kotlin-stdlib:1.6.0'
    api 'dev.xdark:feder:live-SNAPSHOT'
}

publishing {
    repositories {
        maven {
            name 'func'
            url 'https://repo.implario.dev/cristalix'
            credentials {
                username System.getenv('IMPLARIO_REPO_USER')
                password System.getenv('IMPLARIO_REPO_PASSWORD')
            }
        }
    }


    publications {
        mavenBinary(MavenPublication) {
            groupId = 'me.func'
            artifactId = 'animation-api'
            version = 'live-SNAPSHOT'

            from components.java
        }
    }
}

jar {
    archiveBaseName.set 'animation-api'
    from configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

